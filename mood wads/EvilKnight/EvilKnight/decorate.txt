// Evil Knight
actor EvilKnight
{
    Health 250
    Radius 20
    Height 56
    PainChance 100
    Mass 150
    MeleeDamage 4
    MeleeRange 100
    Speed 11
    Scale 0.85

    Monster
    +FLOORCLIP

    SeeSound "dknight/sight"
    ActiveSound "dknight/active"
    MeleeSound "dknight/melee"
    PainSound "dknight/pain"
    DeathSound "dknight/death"
    
    HitObituary "%o was sliced by evil knight."
    Obituary "%o was burned from evil knight's fireballs."
    States
    {
    Spawn:
       KNGT E 5 A_Look
       Loop
    See:
       KNGT AABBCCDD 2 A_Chase
       Loop
    Melee:
       KNGT E 5 A_FaceTarget
       KNGT F 4 A_FaceTarget
       KNGT F 0 A_PlaySound("dknight/swing")
       KNGT FGH 4
       KNGT I 4 A_MeleeAttack
       KNGT J 5
       KNGT E 3
       Goto See
    Missile:
       KNGT E 5 A_FaceTarget
       KNGT F 4 A_FaceTarget
       KNGT FGH 4
       KNGT I 1 A_CustomMissile("KFireBall",32,0,-6)
       KNGT I 1 A_CustomMissile("KFireBall",32,0,-3)
       KNGT I 1 A_CustomMissile("KFireBall",32,0,0)
       KNGT I 1 A_CustomMissile("KFireBall",32,0,3)
       KNGT I 1 A_CustomMissile("KFireBall",32,0,6)
       KNGT I 1 A_CustomMissile("KFireBall",32,0,9)
       KNGT E 3
       Goto See
   Pain:
       KNGT E 3 A_Pain
       KNGT E 3
       Goto See
   Death:
       KNGT K 6 A_Scream
       KNGT LM 5
       KNGT NOPQ 5
       KNGT R 1 A_NoBlocking
       KNGT R -1
       Stop
   }
}
       
actor KFireBall
{
    Radius 4
    Height 4
    Speed 16
    Damage 2
    DamageType "Fire"
    Alpha 0.65
    
    Projectile
    +ROCKETTRAIL
    RenderStyle Translucent
    SeeSound "dknight/attack"
    DeathSound "imp/shotx"
    States
    {
    Spawn:
       FDMB A 2 Bright
       Loop
    Death:
       FDMB BCDE 2 Bright
       Stop
    }
}
