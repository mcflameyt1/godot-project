ACTOR 40mmGrenadeLauncher : Weapon //29010
{
	Inventory.PickupMessage "You got the 40mm Grenade Launcher!."
	Weapon.Ammogive 10
	Weapon.AmmoType1 "40mmGrenades"
	Weapon.AmmoType2 "40mmGrenades"
	Weapon.AmmoUse1 1
	Weapon.AmmoUse2 1
	Weapon.SelectionOrder 400
	Weapon.SlotNumber 5
	+WEAPON.EXPLOSIVE
	Obituary "%o was blown up by %k's Grenade Launcher."
	States
	{
	Ready:
		RGLG A 1 A_WeaponReady
		Loop
	Select:
		RGLG A 1 A_Raise
		Loop
	Deselect:
		RGLG A 1 A_Lower
		Loop
	Fire:
		RGLF A 3
		RGLF A 0 A_GunFlash 
		RGLF A 0 A_PlayWeaponSound ("40mm/grenfire")
		RGLF B 3
		RGLF B 0 A_FireCustomMissile ("40mmShot", 0, 1, 1, 10, 1)
		RGLG ABC 3
		RGLG A 15 
		RGLG A 5 A_ReFire
		Goto Ready
	AltFire:
		RGLF A 3
		RGLF A 0 A_GunFlash 
		RGLF A 0 A_PlayWeaponSound ("40mm/grenfire")
		RGLF B 3
		RGLF B 0 A_FireCustomMissile ("40mmTimedShot", 0, 1, 1, 10, 1)
		RGLG ABC 3
		RGLG A 30 
		RGLG A 5 A_ReFire
		Goto Ready
	Flash:
		RGLF A 1 BRIGHT A_Light1
		RGLF B 2 BRIGHT A_Light2
		Goto LightDone
	Spawn:
		GLMM A -1
		Stop
	}
}

ACTOR 40mmShot
{
	Radius 8
	Height 8
	Damage 25
	Speed 50
	Scale 0.75
	SeeSound "40mm/grenbounce"
	DeathSound "40mm/grenexplode"
	Projectile
	-NOGRAVITY
	States
	{
	Spawn:
		SHRP O 3 bright A_SpawnItemEX("GrenadeSmoke",0,0,0,0,0,0,0,2)
		SHRP O 0 A_CountDown
		Loop
	Death:
		MIS6 B 0 A_NoGravity
		MIS6 B 3 bright A_SetTranslucent (0.85,1)
		MIS6 C 3 bright A_Explode
		MIS6 DEF 3 bright 
		Stop
	}
}

ACTOR 40mmTimedShot : 40mmShot
{
	ReactionTime 30
	Speed 30
	Damage 1
	+HEXENBOUNCE
	+BOUNCEONACTORS
	+EXTREMEDEATH
	+CANBOUNCEWATER
	States
	{
	Death:
		MIS6 B 0 A_NoGravity
		MIS6 B 3 bright A_SetTranslucent (0.85,1)
		MIS6 C 3 bright A_Explode //(200, 200, 0)
		MIS6 DEF 3 bright 
		Stop
	}
}

ACTOR GrenadeSmoke
{   
   Radius 0
   Height 1
   Speed 0
   Projectile
   Scale 0.75
   Renderstyle Translucent
   Alpha 0.45
   States
   {
   Spawn:
      NULL A 3 bright
      SMK2 ABCDE 2 Bright
      Stop 
   }
}

ACTOR 40mmGrenades : Ammo //5567
{
	Inventory.Amount 5
	Inventory.MaxAmount 50
	Ammo.BackpackAmount 20
	Ammo.BackpackMaxAmount 100
	Inventory.PickupSound "misc/i_pkup"
	Inventory.PickupMessage "Picked up some 40mm grenades."
	Inventory.Icon "40MMA0"
	States
	{
	Spawn:
		40MM A -1
		Stop
	}
}

ACTOR 40mmGrenadeBox : 40mmGrenades //5568
{
	Inventory.Amount 20
	Inventory.PickupMessage "Picked up a box of 40mm grenades."
	States
	{
	Spawn:
		40BX A -1
		Stop
	}
}