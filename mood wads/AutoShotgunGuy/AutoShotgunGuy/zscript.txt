Class ASGGuy : Actor
{
  int ammo;

  Default
  {
    Health 90;
    Radius 20;
    Height 56;
    Speed 8;
    PainChance 128;
    SeeSound "grunt/sight";
    AttackSound "asgguy/asgfir";
    PainSound "grunt/pain";
    DeathSound "grunt/death";
    ActiveSound "grunt/active";
    Obituary "%o got emmenthal'd by an ASG guy.";
    DropItem "Shell";
    MONSTER;
    +FLOORCLIP
  }

  States
  {
  Spawn:
    ASGZ AB 10 A_Look();
    Loop;
  See:
    ASGZ AABBCCDD 4 A_Chase();
    Loop;
  Missile:
    TNT1 A 0 A_JumpIf (ammo>=16, "Reload");
    ASGZ E 4 A_FaceTarget();
    ASGZ F 5 Bright ASGFire();
    ASGZ E 6;
    ASGZ E 4 ASGCheckReload();
    ASGZ F 5 Bright ASGFire();
    ASGZ E 6;
    ASGZ E 4 ASGCheckReload();
    ASGZ F 5 Bright ASGFire();
    ASGZ E 6;
    ASGZ E 4 ASGCheckReload();
    ASGZ F 5 Bright ASGFire();
    ASGZ E 6;
    TNT1 A 0 A_PlaySound("asgguy/asgld1");
    TNT1 A 0 A_JumpIf (ammo>=16, "Reload");
    Goto See;
  Reload:
    	ASGZ E 48
     {
       bNoPain=true;
       A_PlaySound("asgguy/asgout");
       ammo=0;
     }
    	ASGZ E 8 A_PlaySound("asgguy/asgin");
    	TNT1 A 0 { bNoPain=false; }
    	Goto See;
  Pain:
    ASGZ G 3;
    ASGZ G 3 A_Pain();
    Goto See;
  Death:
    ASGZ H 5;
    ASGZ I 5 A_Scream();
    ASGZ J 5 A_Fall();
    ASGZ KLM 5;
    ASGZ N -1;
    Stop;
  XDeath:
    ASGZ O 5;
    ASGZ P 5 A_XScream();
    ASGZ Q 5 A_Fall();
    ASGZ RSTUV 5;
    ASGZ W -1;
    Stop;
  Raise:
    ASGZ NMLKJIH 5;
    Goto See;
  }

  void ASGFire()
  {
    A_CustomBulletAttack (5, 4, 3, 4, "BulletPuff", 0);
    ammo++;
  }

  state ASGCheckReload()
  {
    if( ammo>=16 )
      return ResolveState("Reload");
    else
    {
      A_PlaySound("asgguy/asgld1");
      A_CPosRefire();
      return ResolveState(null);
    }
  }
}
